%description:
Test arrival manager: network coded splits ARQ timeouts
If a loss ARQ is planned (waiting to be sent, but held because
verificaiton is going on), and the missing flits arrive during this period,
the ARQ should be canceled.

%inifile: omnetpp.ini
[General]
sim-time-limit = 500ns
**.cmdenv-log-level = debug

**.networkCoding = true
**.generationSize = 2

**.am.arqLimit = 1
**.am.arqIssueTimeout = 4
**.am.arqAnswerTimeoutBase = 5
**.am.lastArqWaitForOngoingVerifications = true

**.awaitSendRequests = true

**.encUnits = 5
**.authUnits = 5
**.busyCyclesEnc = 4
**.busyCyclesAuth = 8
**.encUnit[*].computeCycles = 4
**.authUnit[*].computeCycles = 8

%file: test.ned
import HaecComm.Core.Clock;
import HaecComm.Buffers.PacketQueue;
import HaecComm.MW.ArrivalManagerSplit;
import HaecComm.MW.Crypto.*;

network Test {
    parameters:
        bool networkCoding;
        int generationSize;
        int rows = 4;
        int columns = 4;
        int id = 0;
        int encUnits;
        int authUnits;
        double clockPeriod @unit(s) = 2ns;
    submodules:
        clock: Clock { inter = clockPeriod; }
        am: ArrivalManagerSplit;
        appQ: PacketQueue;
        authQ: PacketQueue;
        netQ: PacketQueue;
        entryGuard: EntryGuardSplit {
            gates:
                encOut[encUnits];
                authOut[authUnits];
        }
        encUnit[encUnits]: EncSplit;
        authUnit[authUnits]: AuthSplit;
        exitGuard: ExitGuardSplit {
            gates:
                encIn[encUnits];
                authIn[authUnits];
        }
        testapp: AMSplitG2C3RemovePlannedARQTestApp;
    connections:
        testapp.appIn <-- am.appOut;
        testapp.arqIn <-- am.arqOut;
        testapp.netOut --> am.netIn;

        am.cryptoOut --> netQ.in;
        entryGuard.netIn <-- netQ.out;

        testapp.appQOut --> appQ.in;
        entryGuard.appIn <-- appQ.out;

        exitGuard.entryOut --> authQ.in;
        entryGuard.exitIn <-- authQ.out;

        for i=0..encUnits-1 {
            encUnit[i].in <-- entryGuard.encOut[i];
            encUnit[i].out --> exitGuard.encIn[i];
        }

        for i=0..authUnits-1 {
            authUnit[i].in <-- entryGuard.authOut[i];
            authUnit[i].out --> exitGuard.authIn[i];
        }

        exitGuard.appOut --> am.cryptoIn;
        exitGuard.netOut --> testapp.retBufIn;
}

%contains-regex: stdout
Initiating planned ARQ.*
Canceling planned ARQ.*

%contains-regex: stdout
9 Got flit flit1-dec
Source \(1,0\)
Target \(0,0\)
GID 123
GEV 42
Mode MODE_SPLIT_NC
.*
12 Got flit flit2-dec
Source \(1,0\)
Target \(0,0\)
GID 123
GEV 43
Mode MODE_SPLIT_NC

%not-contains-regex: stdout
.*Got flit flit3-dec

%not-contains: stdout
.*Got ARQ

%not-contains: stdout
undisposed object:

%not-contains: stdout
-- check module destructor
